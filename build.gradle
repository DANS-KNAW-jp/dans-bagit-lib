apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply from: 'eclipse.gradle'
apply from: 'maven-central.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(project.version == "unspecified"){
  project.version = "5.0-SNAPSHOT"
}

repositories {
  jcenter()
}

dependencies {
  compile 'org.springframework.boot:spring-boot-starter-logging:1.3.2.RELEASE', 
    'org.codehaus.groovy:groovy-all:2.4.0',
    'org.bouncycastle:bcprov-jdk15on:1.52',
    'net.sf.jopt-simple:jopt-simple:4.9'

  testCompile 'junit:junit:4.12'
}

sourceSets.main.resources {
  srcDir "src/assembly"
  include "*.groovy"
}

test { //show test output
  testLogging.showStandardStreams = true
}

jacoco {
  toolVersion = "0.7.5.201505241946" //needed for jenkins to report code coverage correctly
}
jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport

findbugsTest.enabled = false

findbugs {
  ignoreFailures = true //don't fail the gradle build if bugs are found
}

pmd {
  ruleSets = ["java-basic", "java-braces"]
  ignoreFailures = true //don't fail the gradle build if rules are broken
}