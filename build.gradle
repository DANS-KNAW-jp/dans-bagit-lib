apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply from: 'eclipse.gradle'
apply from: 'maven-central.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(project.version == "unspecified"){
  project.version = "5.0-SNAPSHOT"
}

repositories {
  jcenter()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.16'
    
  testCompile 'junit:junit:4.12', 'org.springframework.boot:spring-boot-starter-logging:1.3.2.RELEASE'
}

test { //show test output
  testLogging.showStandardStreams = true
}

jacoco {
  toolVersion = "0.7.5.201505241946" //needed for jenkins to report code coverage correctly
}
jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport

findbugsTest.enabled = false

findbugs {
  ignoreFailures = true //don't fail the gradle build if bugs are found
}

pmdTest.enabled = false

pmd {
  ruleSets = ["java-basic", "java-braces", "java-empty"]
}

uploadArchives {
  repositories {
    mavenDeployer {
      if(!project.hasProperty("ossrhUsername")){ //so travis CI doesn't break...
        project.ext.ossrhUsername = "foo"
        project.ext.ossrhPassword = "foo"
      }

      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'bagit-java'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'The BAGIT LIBRARY is a software library intended to support the creation, manipulation, and validation of bags. Its current version is 0.97. It is version aware with the earliest supported version being 0.93.'
        url 'https://github.com/LibraryOfCongress/bagit-java'

        scm {
          connection 'scm:git:https://github.com/LibraryOfCongress/bagit-java'
          developerConnection 'scm:git:ssh://github.com/LibraryOfCongress/bagit-java'
          url 'https://github.com/LibraryOfCongress/bagit-java'
        }

        licenses {
          license {
            name 'No Copyright'
            url 'https://github.com/LibraryOfCongress/bagit-java/blob/master/LICENSE.txt'
          }
        }

        developers {
          developer {
            id 'johnscancella'
            name 'John Scancella'
            email 'jsca@loc.gov'
          }
        }
      }
    }
  }
}
